cmake_minimum_required(VERSION 3.1)
project(mGBA)
set(BINARY_NAME mgba CACHE INTERNAL "Name of output binaries")
if(NOT MSVC)
	set(GCC_STD "c99")
	if(CMAKE_C_COMPILER_ID STREQUAL "GNU" AND CMAKE_COMPILER_VERSION VERSION_LESS "4.3")
		set(GCC_STD "gnu99")
	endif()
	set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wextra -Wno-missing-field-initializers -std=${GCC_STD}")
else()
	set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -D_CRT_SECURE_NO_WARNINGS /wd4003 /wd4244 /wd4146")
endif()
set(USE_DEBUGGERS ON CACHE BOOL "Whether or not to enable the debugging infrastructure")
if (NOT WIN32)
	set(USE_EDITLINE ON CACHE BOOL "Whether or not to enable the CLI-mode debugger")
endif()
set(USE_GDB_STUB ON CACHE BOOL "Whether or not to enable the GDB stub ARM debugger")
set(USE_FFMPEG ON CACHE BOOL "Whether or not to enable FFmpeg support")
set(USE_ZLIB ON CACHE BOOL "Whether or not to enable zlib support")
set(USE_MINIZIP ON CACHE BOOL "Whether or not to enable external minizip support")
set(USE_PNG ON CACHE BOOL "Whether or not to enable PNG support")
set(USE_LIBZIP ON CACHE BOOL "Whether or not to enable LIBZIP support")
set(USE_MAGICK ON CACHE BOOL "Whether or not to enable ImageMagick support")
set(USE_SQLITE3 ON CACHE BOOL "Whether or not to enable SQLite3 support")
set(USE_ELF ON CACHE BOOL "Whether or not to enable ELF support")
set(M_CORE_GBA ON CACHE BOOL "Build Game Boy Advance core")
set(M_CORE_GB ON CACHE BOOL "Build Game Boy core")
set(USE_LZMA ON CACHE BOOL "Whether or not to enable 7-Zip support")
set(ENABLE_SCRIPTING ON CACHE BOOL "Whether or not to enable scripting support")
set(BUILD_QT ON CACHE BOOL "Build Qt frontend")
set(BUILD_SDL ON CACHE BOOL "Build SDL frontend")
set(BUILD_LIBRETRO OFF CACHE BOOL "Build libretro core")
if(APPLE)
	set(BUILD_OPENEMU OFF CACHE BOOL "Build OpenEmu core")
endif()
set(BUILD_PERF OFF CACHE BOOL "Build performance profiling tool")
set(BUILD_TEST OFF CACHE BOOL "Build testing harness")
set(BUILD_SUITE OFF CACHE BOOL "Build test suite")
set(BUILD_EXAMPLE OFF CACHE BOOL "Build example frontends")
set(BUILD_PYTHON OFF CACHE BOOL "Build Python bindings")
set(BUILD_STATIC OFF CACHE BOOL "Build a static library")
set(BUILD_SHARED ON CACHE BOOL "Build a shared library")
set(SKIP_LIBRARY OFF CACHE BOOL "Skip building the library (useful for only building libretro or OpenEmu cores)")
set(BUILD_GL ON CACHE STRING "Build with OpenGL")
set(BUILD_GLES2 OFF CACHE STRING "Build with OpenGL|ES 2")
set(USE_EPOXY ON CACHE STRING "Build with libepoxy")
set(DISABLE_DEPS OFF CACHE BOOL "Build without dependencies")
if(WIN32)
	set(WIN32_UNIX_PATHS OFF CACHE BOOL "Use Unix-like paths")
	mark_as_advanced(WIN32_UNIX_PATHS)
endif()
file(GLOB ARM_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/arm/*.c)
file(GLOB ARM_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/arm/test/*.c)
file(GLOB LR35902_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/lr35902/*.c)
file(GLOB LR35902_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/lr35902/test/*.c)
file(GLOB GBA_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/*.c)
file(GLOB GBA_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/test/*.c)
file(GLOB GB_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/*.c)
file(GLOB GB_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/test/*.c)
file(GLOB GBA_CHEATS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/cheats/*.c)
file(GLOB GBA_RR_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/rr/*.c)
file(GLOB CORE_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/core/*.c)
file(GLOB CORE_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/core/test/*.c)
file(GLOB UTIL_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/*.[cSs])
file(GLOB UTIL_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/test/*.c)
file(GLOB GUI_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/gui/*.c ${CMAKE_CURRENT_SOURCE_DIR}/src/feature/gui/*.c)
file(GLOB GBA_RENDERER_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/renderers/*.c)
file(GLOB GBA_SIO_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/sio/*.c)
file(GLOB GBA_EXTRA_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gba/extra/*.c)
file(GLOB GB_SIO_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/sio/*.c)
file(GLOB GB_RENDERER_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/renderers/*.c)
file(GLOB GB_EXTRA_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/extra/*.c)
file(GLOB THIRD_PARTY_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/inih/*.c)
file(GLOB EXTRA_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/feature/*.c)
set(CORE_VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-mem.c ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-fifo.c)
set(VFS_SRC)
source_group("ARM core" FILES ${ARM_SRC})
source_group("LR35902 core" FILES ${LR35902_SRC})
source_group("GBA board" FILES ${GBA_SRC} ${GBA_RENDERER_SRC} ${GBA_SIO_SRC})
source_group("GBA extra" FILES ${GBA_CHEATS_SRC} ${GBA_RR_SRC})
source_group("GB board" FILES ${GB_SRC} ${GB_SIO_SRC})
source_group("Utilities" FILES ${UTIL_SRC})
include_directories(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/include ${CMAKE_CURRENT_SOURCE_DIR}/src)

if(NOT CMAKE_BUILD_TYPE)
	set(CMAKE_BUILD_TYPE Release CACHE STRING "Build type (e.g. Release or Debug)" FORCE)
endif()

if(NOT WIN32 OR WIN32_UNIX_PATHS)
	include(GNUInstallDirs)
else()
	set(CMAKE_INSTALL_LIBDIR ".")
	set(CMAKE_INSTALL_BINDIR ".")
	set(CMAKE_INSTALL_DATADIR ".")
	set(CMAKE_INSTALL_DOCDIR ".")
	set(CMAKE_INSTALL_INCLUDEDIR "include")
endif()

set(LIBDIR "${CMAKE_INSTALL_LIBDIR}" CACHE PATH "Installed library directory")
mark_as_advanced(LIBDIR)

if (BUILD_LIBRETRO)
	set(LIBRETRO_LIBDIR "${LIBDIR}" CACHE PATH "Installed library directory (Libretro)")
	mark_as_advanced(LIBRETRO_LIBDIR)
endif()

if (BUILD_OPENEMU)
	set(OE_LIBDIR "${LIBDIR}" CACHE PATH "Installed library directory (OpenEmu)")
	mark_as_advanced(OE_LIBDIR)
endif()


set(CMAKE_INSTALL_RPATH "${LIBDIR}")

if (NOT DEFINED MANDIR)
	set(MANDIR ${CMAKE_INSTALL_MANDIR})
endif()

# Function definitions
include(FindPkgConfig)
function(find_feature FEATURE_NAME FEATURE_REQUIRES)
	if (NOT ${FEATURE_NAME})
		return()
	endif()
	if (DISABLE_DEPS)
		set(${FEATURE_NAME} OFF PARENT_SCOPE)
		return()
	endif()
	foreach(REQUIRE ${FEATURE_REQUIRES})
		if(NOT ${REQUIRE}_FOUND)
			find_package(${REQUIRE} QUIET)
			if(NOT ${REQUIRE}_FOUND)
				pkg_search_module(${REQUIRE} ${REQUIRE})
				if (NOT ${REQUIRE}_FOUND)
					message(WARNING "Requested module ${REQUIRE} missing for feature ${FEATURE_NAME}. Feature disabled.")
					set(${FEATURE_NAME} OFF PARENT_SCOPE)
					return()
				endif()
			endif()
		endif()
		string(TOUPPER ${REQUIRE} UREQUIRE)
		set(${UREQUIRE}_CFLAGS_OTHER ${${REQUIRE}_CFLAGS_OTHER} PARENT_SCOPE)
		set(${UREQUIRE}_FOUND ${${REQUIRE}_FOUND} PARENT_SCOPE)
		set(${UREQUIRE}_INCLUDE_DIRS ${${REQUIRE}_INCLUDE_DIRS} PARENT_SCOPE)
		set(${UREQUIRE}_VERSION_STRING ${${REQUIRE}_VERSION_STRING} PARENT_SCOPE)
		if (APPLE)
			set(IS_FRAMEWORK OFF)
			set(LIBS)
			foreach(LIB IN LISTS ${REQUIRE}_LIBRARIES)
				if(LIB STREQUAL "-framework")
					set(IS_FRAMEWORK ON)
				elseif(IS_FRAMEWORK)
					list(APPEND LIBS "-framework ${LIB}")
					set(IS_FRAMEWORK OFF)
				else()
					list(APPEND LIBS ${LIB})
				endif()
			endforeach()
			set(${UREQUIRE}_LIBRARIES ${LIBS} PARENT_SCOPE)
		else()
			set(${UREQUIRE}_LIBRARIES ${${REQUIRE}_LIBRARIES} PARENT_SCOPE)
		endif()
		set(${UREQUIRE}_LIBRARY_DIRS ${${REQUIRE}_LIBRARY_DIRS} PARENT_SCOPE)
		set(${UREQUIRE}_LDFLAGS_OTHER ${${REQUIRE}_LDFLAGS_OTHER} PARENT_SCOPE)
	endforeach()
endfunction()

# Version information
add_custom_target(version-info ALL
	COMMAND ${CMAKE_COMMAND}
	-DBINARY_NAME=${BINARY_NAME}
	-DCONFIG_FILE=${CMAKE_CURRENT_SOURCE_DIR}/src/core/version.c.in
	-DOUT_FILE=${CMAKE_CURRENT_BINARY_DIR}/version.c
	-P ${CMAKE_CURRENT_SOURCE_DIR}/version.cmake
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})

include(${CMAKE_CURRENT_SOURCE_DIR}/version.cmake)
configure_file(${CMAKE_CURRENT_SOURCE_DIR}/src/core/version.c.in ${CMAKE_CURRENT_BINARY_DIR}/version.c)

list(APPEND UTIL_SRC ${CMAKE_CURRENT_BINARY_DIR}/version.c)
source_group("Generated sources" FILES ${CMAKE_CURRENT_BINARY_DIR}/version.c)

# Advanced settings
if(NOT DEFINED 3DS AND NOT (CMAKE_C_COMPILER_ID STREQUAL "GNU" AND CMAKE_COMPILER_VERSION VERSION_LESS "4.5"))
	# LTO appears to make 3DS binary slower
	set(DEFAULT_LTO ON)
else()
	set(DEFAULT_LTO OFF)
endif()

set(BUILD_LTO ${DEFAULT_LTO} CACHE BOOL "Build with link-time optimization")
set(BUILD_PGO OFF CACHE BOOL "Build with profiling-guided optimization")
set(PGO_STAGE_2 CACHE BOOL "Rebuild for profiling-guided optimization after profiles have been generated")
set(PGO_DIR "/tmp/gba-pgo/" CACHE PATH "Profiling-guided optimization profiles path")
mark_as_advanced(BUILD_LTO BUILD_PGO PGO_STAGE_2 PGO_DIR)
set(PGO_PRE_FLAGS "-fprofile-generate=${PGO_DIR} -fprofile-arcs")
set(PGO_POST_FLAGS "-fprofile-use=${PGO_DIR} -fbranch-probabilities")

if(BUILD_PGO AND NOT PGO_STAGE_2)
	set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${PGO_PRE_FLAGS}")
	set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${PGO_PRE_FLAGS}")
	set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${PGO_PRE_FLAGS}")
elseif(BUILD_PGO AND PGO_STAGE_2)
	set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${PGO_POST_FLAGS}")
	set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${PGO_POST_FLAGS}")
	set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${PGO_POST_FLAGS}")
endif()

# Platform support
if(WIN32)
	set(WIN32_VERSION "${LIB_VERSION_MAJOR},${LIB_VERSION_MINOR},${LIB_VERSION_PATCH}")
	add_definitions(-D_WIN32_WINNT=0x0600)
	list(APPEND OS_LIB ws2_32 shlwapi)
	list(APPEND CORE_VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-fd.c ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/windows/vfs-w32.c)
	file(GLOB OS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/windows/*.c)
	source_group("Windows-specific code" FILES ${OS_SRC})
	if(MSVC)
		add_definitions(-DNOMINMAX -DWIN32_LEAN_AND_MEAN)
		set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS TRUE)
	endif()
elseif(UNIX)
	set(USE_PTHREADS ON)
	add_definitions(-DUSE_PTHREADS)

	if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
		add_definitions(-D_GNU_SOURCE)
	endif()
	if(NOT APPLE AND NOT HAIKU)
		set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pthread")
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pthread")
	endif()

	list(APPEND CORE_VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-fd.c ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-dirent.c)
	file(GLOB OS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/posix/*.c)
	source_group("POSIX-specific code" FILES ${OS_SRC})
endif()

if(APPLE)
	add_definitions(-D_DARWIN_C_SOURCE)
	if(CMAKE_SYSTEM_VERSION VERSION_GREATER "10.5.8")
		set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mmacosx-version-min=10.6")
	endif()
endif()

if(NOT HAIKU AND NOT MSVC AND NOT PSP2)
	set(M_LIBRARY m)
endif()
list(APPEND OS_LIB ${M_LIBRARY})

if(APPLE OR CMAKE_C_COMPILER_ID STREQUAL "GNU" AND BUILD_LTO)
	set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -flto")
	set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -flto")
endif()

if(BUILD_BBB OR BUILD_RASPI OR BUILD_PANDORA)
	if(NOT BUILD_EGL)
		add_definitions(-DCOLOR_16_BIT -DCOLOR_5_6_5)
	endif()
endif()

if(BUILD_RASPI)
	set(BUILD_GL OFF CACHE BOOL "OpenGL not supported" FORCE)
endif()

if(BUILD_PANDORA)
	add_definitions(-DBUILD_PANDORA)
endif()

if(CMAKE_SYSTEM_PROCESSOR MATCHES "arm.*")
	enable_language(ASM)
endif()

if(PSP2 OR WII)
	set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-format")
endif()

if(DEFINED 3DS OR DEFINED PSP2 OR DEFINED WII)
	set(USE_DEBUGGERS OFF)
	set(USE_SQLITE3 OFF)
endif()

if(DEFINED 3DS)
	add_definitions(-DFIXED_ROM_BUFFER)
endif()

if(NOT M_CORE_GBA)
	set(USE_GDB_STUB OFF)
endif()

if(NOT USE_DEBUGGERS)
	set(USE_EDITLINE OFF)
	set(USE_GDB_STUB OFF)
endif()

if(WII)
	add_definitions(-U__STRICT_ANSI__)
endif()

if(DEFINED 3DS)
	add_definitions(-D_GNU_SOURCE)
endif()

include(CheckFunctionExists)
check_function_exists(strdup HAVE_STRDUP)
check_function_exists(strndup HAVE_STRNDUP)
if(NOT DEFINED PSP2)
	check_function_exists(localtime_r HAVE_LOCALTIME_R)
endif()
if(NOT CMAKE_SYSTEM_NAME STREQUAL "Generic")
	check_function_exists(snprintf_l HAVE_SNPRINTF_L)
	if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
		# The strtof_l on Linux not actually exposed nor actually strtof_l
		set(HAVE_STRTOF_L OFF)
	else()
		check_function_exists(strtof_l HAVE_STRTOF_L)
	endif()
	check_function_exists(newlocale HAVE_NEWLOCALE)
	check_function_exists(freelocale HAVE_FREELOCALE)
	check_function_exists(uselocale HAVE_USELOCALE)
	check_function_exists(setlocale HAVE_SETLOCALE)
else()
	if(DEFINED 3DS OR DEFINED WII)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,snprintf_l)
		check_function_exists(snprintf_l HAVE_SNPRINTF_L)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,strtof_l)
		check_function_exists(strtof_l HAVE_STRTOF_L)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,newlocale)
		check_function_exists(newlocale HAVE_NEWLOCALE)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,freelocale)
		check_function_exists(freelocale HAVE_FREELOCALE)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,uselocale)
		check_function_exists(uselocale HAVE_USELOCALE)
		set(CMAKE_REQUIRED_FLAGS -Wl,--require-defined,setlocale)
		check_function_exists(setlocale HAVE_SETLOCALE)
		unset(CMAKE_REQUIRED_FLAGS)
	endif()
	if(NOT DEFINED 3DS AND NOT DEFINED PSP2 AND NOT DEFINED WII)
		set(DISABLE_DEPS ON CACHE BOOL "This platform cannot build with dependencies" FORCE)
	endif()
	set(DISABLE_FRONTENDS ON)
	set(MINIMAL_CORE ON)
	set(ENABLE_EXTRA ON)
endif()

check_function_exists(chmod HAVE_CHMOD)
check_function_exists(umask HAVE_UMASK)

set(FUNCTION_DEFINES)

if(HAVE_STRDUP)
	list(APPEND FUNCTION_DEFINES HAVE_STRDUP)
endif()

if(HAVE_STRNDUP)
	list(APPEND FUNCTION_DEFINES HAVE_STRNDUP)
endif()

if(HAVE_LOCALTIME_R)
	list(APPEND FUNCTION_DEFINES HAVE_LOCALTIME_R)
endif()

if(HAVE_NEWLOCALE AND HAVE_FREELOCALE AND HAVE_USELOCALE OR APPLE)
	list(APPEND FUNCTION_DEFINES HAVE_LOCALE)
	if (HAVE_STRTOF_L)
		list(APPEND FUNCTION_DEFINES HAVE_STRTOF_L)
	endif()
	if (HAVE_SNPRINTF_L)
		list(APPEND FUNCTION_DEFINES HAVE_SNPRINTF_L)
	endif()
endif()

if(HAVE_SETLOCALE)
	list(APPEND FUNCTION_DEFINES HAVE_SETLOCALE)
endif()

if(HAVE_CHMOD)
	list(APPEND FUNCTION_DEFINES HAVE_CHMOD)
endif()

if(HAVE_UMASK)
	list(APPEND FUNCTION_DEFINES HAVE_UMASK)
endif()

# Feature dependencies
set(FEATURE_DEFINES)
set(FEATURE_FLAGS)
set(FEATURES)
set(ENABLES)
if(CMAKE_SYSTEM_NAME MATCHES .*BSD)
	set(LIBEDIT_LIBRARIES -ledit)
	if (CMAKE_SYSTEM_NAME STREQUAL OpenBSD)
		list(APPEND LIBEDIT_LIBRARIES -ltermcap)
	endif()
else()
	find_feature(USE_EDITLINE "libedit")
endif()
if(BUILD_GL)
	find_package(OpenGL QUIET)
	if(NOT OPENGL_FOUND)
		set(BUILD_GL OFF CACHE BOOL "OpenGL not found" FORCE)
	endif()
endif()
if(NOT BUILD_GL)
	set(OPENGLE_LIBRARY "" CACHE PATH "" FORCE)
endif()
if(BUILD_GLES2 AND NOT BUILD_RASPI)
	find_path(OPENGLES2_INCLUDE_DIR NAMES GLES2/gl2.h)
	find_library(OPENGLES2_LIBRARY NAMES GLESv2 GLESv2_CM)
	if(NOT OPENGLES2_INCLUDE_DIR OR NOT OPENGLES2_LIBRARY)
		set(BUILD_GLES2 OFF CACHE BOOL "OpenGL|ES 2 not found" FORCE)
	endif()
endif()
if(NOT BUILD_GLES2)
	set(OPENGLES2_LIBRARY "" CACHE PATH "" FORCE)
endif()
set(WANT_ZLIB ${USE_ZLIB})
set(WANT_PNG ${USE_PNG})
set(WANT_LIBZIP ${USE_LIBZIP})
set(WANT_SQLITE3 ${USE_SQLITE3})
set(USE_CMOCKA ${BUILD_SUITE})

find_feature(USE_FFMPEG "libavcodec;libavformat;libavresample;libavutil;libswscale")
find_feature(USE_ZLIB "ZLIB")
find_feature(USE_MINIZIP "minizip")
find_feature(USE_PNG "PNG")
find_feature(USE_LIBZIP "libzip")
find_feature(USE_MAGICK "MagickWand")
find_feature(USE_EPOXY "epoxy")
find_feature(USE_CMOCKA "cmocka")
find_feature(USE_SQLITE3 "sqlite3")
find_feature(USE_ELF "libelf")
find_feature(ENABLE_PYTHON "PythonLibs")

# Features
set(DEBUGGER_SRC
	${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/debugger.c
	${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/parser.c
	${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/symbols.c
	${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/cli-debugger.c)

file(GLOB DEBUGGER_TEST_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/test/*.c)

set(FEATURE_SRC)
set(CPACK_DEBIAN_PACKAGE_DEPENDS "libc6")

if(DISABLE_DEPS)
	set(USE_GDB_STUB OFF)
endif()

if(USE_EDITLINE)
	list(APPEND FEATURES EDITLINE)
	include_directories(AFTER ${LIBEDIT_INCLUDE_DIRS})
	link_directories(${LIBEDIT_LIBRARY_DIRS})
	set(DEBUGGER_LIB ${LIBEDIT_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libedit2")
	list(APPEND FEATURE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/feature/editline/cli-el-backend.c")
else()
	set(DEBUGGER_LIB "")
endif()

if(USE_GDB_STUB)
	list(APPEND FEATURES GDB_STUB)
	list(APPEND FEATURE_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/debugger/gdb-stub.c)
endif()
source_group("Debugger" FILES ${DEBUGGER_SRC})

if(USE_FFMPEG)
	list(APPEND FEATURES FFMPEG)
	pkg_search_module(LIBSWRESAMPLE QUIET libswresample)
	if(NOT LIBSWRESAMPLE_FOUND)
		list(APPEND FEATURES LIBAV)
	endif()
	include_directories(AFTER ${LIBAVCODEC_INCLUDE_DIRS} ${LIBAVFORMAT_INCLUDE_DIRS} ${LIBAVRESAMPLE_INCLUDE_DIRS} ${LIBAVUTIL_INCLUDE_DIRS} ${LIBSWSCALE_INCLUDE_DIRS})
	link_directories(${LIBAVCODEC_LIBRARY_DIRS} ${LIBAVFORMAT_LIBRARY_DIRS} ${LIBAVRESAMPLE_LIBRARY_DIRS} ${LIBAVUTIL_LIBRARY_DIRS} ${LIBSWSCALE_LIBRARY_DIRS})
	list(APPEND FEATURE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/feature/ffmpeg/ffmpeg-encoder.c")
	string(REGEX MATCH "^[0-9]+" LIBAVCODEC_VERSION_MAJOR ${libavcodec_VERSION})
	string(REGEX MATCH "^[0-9]+" LIBAVFORMAT_VERSION_MAJOR ${libavformat_VERSION})
	string(REGEX MATCH "^[0-9]+" LIBAVRESAMPLE_VERSION_MAJOR ${libavresample_VERSION})
	string(REGEX MATCH "^[0-9]+" LIBAVUTIL_VERSION_MAJOR ${libavutil_VERSION})
	string(REGEX MATCH "^[0-9]+" LIBSWSCALE_VERSION_MAJOR ${libswscale_VERSION})
	list(APPEND DEPENDENCY_LIB ${LIBAVCODEC_LIBRARIES} ${LIBAVFORMAT_LIBRARIES} ${LIBAVRESAMPLE_LIBRARIES} ${LIBAVUTIL_LIBRARIES} ${LIBSWSCALE_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libavcodec${LIBAVCODEC_VERSION_MAJOR}|libavcodec-extra-${LIBAVCODEC_VERSION_MAJOR}|libavcodec-ffmpeg${LIBAVCODEC_VERSION_MAJOR}|libavcodec-ffmpeg-extra${LIBAVCODEC_VERSION_MAJOR}")
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libavformat${LIBAVFORMAT_VERSION_MAJOR}|libavformat-ffmpeg${LIBAVFORMAT_VERSION_MAJOR}")
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libavresample${LIBAVRESAMPLE_VERSION_MAJOR}|libavresample-ffmpeg${LIBAVRESAMPLE_VERSION_MAJOR}")
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libavutil${LIBAVUTIL_VERSION_MAJOR}|libavutil-ffmpeg${LIBAVUTIL_VERSION_MAJOR}")
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libswscale${LIBSWSCALE_VERSION_MAJOR}|libswscale-ffmpeg${LIBSWSCALE_VERSION_MAJOR}")
	set(CPACK_DEBIAN_PACKAGE_RECOMMENDS "libavcodec-extra|libavcodec-ffmpeg-extra${LIBAVCODEC_VERSION_MAJOR}")
	if(APPLE)
		set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -framework VideoDecodeAcceleration -framework CoreVideo")
		set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -framework VideoDecodeAcceleration -framework CoreVideo")
	endif()
endif()

list(APPEND THIRD_PARTY_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/blip_buf/blip_buf.c")

if(USE_MAGICK)
	list(APPEND FEATURES MAGICK)
	include_directories(AFTER ${MAGICKWAND_INCLUDE_DIRS})
	link_directories(${MAGICKWAND_LIBRARY_DIRS})
	list(APPEND FEATURE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/feature/imagemagick/imagemagick-gif-encoder.c")
	list(APPEND DEPENDENCY_LIB ${MAGICKWAND_LIBRARIES})
	string(REGEX MATCH "^[0-9]+\\.[0-9]+" MAGICKWAND_VERSION_PARTIAL ${MagickWand_VERSION})
	string(REGEX MATCH "^[0-9]+" MAGICKWAND_VERSION_MAJOR ${MagickWand_VERSION})
	if(${MAGICKWAND_VERSION_PARTIAL} EQUAL "6.7")
		set(MAGICKWAND_DEB_VERSION "5")
	elseif(${MagickWand_VERSION} EQUAL "6.9.7")
		set(MAGICKWAND_DEB_VERSION "-6.q16-3")
	else()
		set(MAGICKWAND_DEB_VERSION "-6.q16-2")
	endif()
	list(APPEND FEATURE_DEFINES MAGICKWAND_VERSION_MAJOR=${MAGICKWAND_VERSION_MAJOR})
	list(APPEND FEATURE_FLAGS ${MAGICKWAND_CFLAGS_OTHER})

	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libmagickwand${MAGICKWAND_DEB_VERSION}")
endif()

if(WANT_ZLIB AND NOT USE_ZLIB)
	set(SKIP_INSTALL_ALL ON)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib zlib)
	set_property(TARGET zlibstatic PROPERTY INCLUDE_DIRECTORIES ${CMAKE_CURRENT_BINARY_DIR}/zlib;${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib)
	set_property(TARGET zlib PROPERTY EXCLUDE_FROM_ALL ON)
	set_property(TARGET example PROPERTY EXCLUDE_FROM_ALL ON)
	set_property(TARGET minigzip PROPERTY EXCLUDE_FROM_ALL ON)
	set(ZLIB_INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib ${CMAKE_CURRENT_BINARY_DIR}/zlib)
	set(ZLIB_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib ${CMAKE_CURRENT_BINARY_DIR}/zlib)
	set(ZLIB_LIBRARY zlibstatic)
	list(APPEND DEPENDENCY_LIB zlibstatic)
	set(USE_ZLIB ON)
endif()

if(USE_ZLIB)
	list(APPEND FEATURES ZLIB)
	include_directories(AFTER ${ZLIB_INCLUDE_DIRS})
	list(APPEND DEPENDENCY_LIB ${ZLIB_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},zlib1g")
	set(HAVE_CRC32 ON)
	list(APPEND OS_LIB ${ZLIB_LIBRARIES})
else()
	# zlib pulls in crc32
	check_function_exists(crc32 HAVE_CRC32)
endif()

if(HAVE_CRC32)
	list(APPEND FUNCTION_DEFINES HAVE_CRC32)
endif()

if(WANT_PNG AND USE_ZLIB AND NOT USE_PNG)
	set(PNG_STATIC ON CACHE BOOL "" FORCE)
	set(PNG_SHARED OFF CACHE BOOL "" FORCE)
	set(PNG_TESTS OFF CACHE BOOL "" FORCE)
	set(SKIP_INSTALL_ALL ON)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/libpng libpng)
	set_property(TARGET png16_static PROPERTY INCLUDE_DIRECTORIES ${CMAKE_CURRENT_BINARY_DIR}/libpng;${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/libpng;${ZLIB_INCLUDE_DIRS})
	set(PNG_INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/libpng ${CMAKE_CURRENT_BINARY_DIR}/libpng)
	list(APPEND DEPENDENCY_LIB png16_static)
	set(USE_PNG ON)
endif()

if(USE_PNG)
	list(APPEND FEATURES PNG)
	include_directories(AFTER ${PNG_INCLUDE_DIRS})
	list(APPEND DEPENDENCY_LIB ${PNG_LIBRARIES} ${ZLIB_LIBRARIES})
	if(PNG_VERSION_STRING)
		string(REGEX MATCH "^[0-9]+\\.[0-9]+" PNG_VERSION_PARTIAL ${PNG_VERSION_STRING})
		if(${PNG_VERSION_PARTIAL} STREQUAL "1.6")
			set(PNG_DEB_VERSION "16-16")
		else()
			set(PNG_DEB_VERSION "12-0")
		endif()
		set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libpng${PNG_DEB_VERSION}")
	endif()
endif()

if(WANT_SQLITE3 AND NOT USE_SQLITE3)
	list(APPEND FEATURE_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/sqlite3/sqlite3.c)
	include_directories(AFTER ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/sqlite3/)
	set(USE_SQLITE3 ON)
endif()

if(USE_LIBZIP)
	include_directories(AFTER ${LIBZIP_INCLUDE_DIRS})
	link_directories(${LIBZIP_LIBRARY_DIRS})
	list(APPEND DEPENDENCY_LIB ${LIBZIP_LIBRARIES})
	list(APPEND FEATURES LIBZIP)
	list(APPEND VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-zip.c)
	string(REGEX MATCH "^[0-9]+" LIBZIP_VERSION_MAJOR ${libzip_VERSION})
	if (LIBZIP_VERSION_MAJOR LESS 1)
		set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libzip2")
	elseif(LIBZIP_VERSION_MAJOR EQUAL 1)
		set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libzip4")
	else()
		message(AUTHOR_WARNING Unknown version of libzip detected: ${libzip_VERSION})
	endif()
elseif(USE_MINIZIP)
	include_directories(AFTER ${MINIZIP_INCLUDE_DIRS})
	link_directories(${MINIZIP_LIBRARY_DIRS})
	list(APPEND DEPENDENCY_LIB ${MINIZIP_LIBRARIES})
	list(APPEND FEATURES MINIZIP)
	list(APPEND VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-zip.c)
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libminizip1")
elseif(USE_ZLIB)
	list(APPEND VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-zip.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib/contrib/minizip/ioapi.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib/contrib/minizip/unzip.c)
	if(NOT MSVC)
		set_source_files_properties(
			${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib/contrib/minizip/ioapi.c
			${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/zlib/contrib/minizip/unzip.c
			PROPERTIES COMPILE_FLAGS "-Wno-unused-parameter -Wno-implicit-function-declaration")
	endif()
endif()

if (USE_LZMA)
	include_directories(AFTER ${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma)
	add_definitions(-D_7ZIP_PPMD_SUPPPORT)
	list(APPEND VFS_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/util/vfs/vfs-lzma.c)
	set(LZMA_SRC
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zAlloc.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zArcIn.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zBuf.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zBuf2.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zCrc.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zCrcOpt.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zDec.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/CpuArch.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Delta.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/LzmaDec.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Lzma2Dec.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Bra.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Bra86.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/BraIA64.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Bcj2.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Ppmd7.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/Ppmd7Dec.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zFile.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/third-party/lzma/7zStream.c)
	list(APPEND VFS_SRC ${LZMA_SRC})
	list(APPEND FEATURES LZMA)
endif()

if(USE_EPOXY)
	add_definitions(-DBUILD_GL -DBUILD_GLES2)
	list(APPEND FEATURES EPOXY)
	include_directories(AFTER ${EPOXY_INCLUDE_DIRS})
	set(OPENGLES2_LIBRARY ${EPOXY_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libepoxy0")
endif()

if(USE_SQLITE3)
	list(APPEND FEATURES SQLITE3)
	include_directories(AFTER ${SQLITE3_INCLUDE_DIRS})
	list(APPEND DEPENDENCY_LIB ${SQLITE3_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libsqlite3-0")
	list(APPEND FEATURE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/src/feature/sqlite3/no-intro.c")
endif()

if(USE_ELF)
	list(APPEND FEATURES ELF)
	include_directories(AFTER ${LIBELF_INCLUDE_DIRS})
	list(APPEND DEPENDENCY_LIB ${LIBELF_LIBRARIES})
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "${CPACK_DEBIAN_PACKAGE_DEPENDS},libelfg0")
endif()

if(ENABLE_SCRIPTING)
	list(APPEND ENABLES SCRIPTING)

	if(BUILD_PYTHON)
		find_package(PythonLibs ${USE_PYTHON_VERSION})
		list(APPEND DEPENDENCY_LIB ${PYTHON_LIBRARIES})
		include_directories(AFTER ${PYTHON_INCLUDE_DIRS})
		list(APPEND ENABLES PYTHON)
	endif()
endif()

set(TEST_SRC ${CORE_TEST_SRC})
if(M_CORE_GB)
	add_definitions(-DM_CORE_GB)
	list(APPEND CORE_SRC
		${LR35902_SRC}
		${GB_SRC}
		${GB_RENDERER_SRC})
	list(APPEND DEBUGGER_SRC
		${CMAKE_CURRENT_SOURCE_DIR}/src/lr35902/debugger/cli-debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/lr35902/debugger/debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/lr35902/debugger/memory-debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/gb/debugger/cli.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/gb/debugger/symbols.c)
	list(APPEND TEST_SRC
		${LR35902_TEST_SRC}
		${GB_TEST_SRC})
endif()

if(M_CORE_GBA)
	add_definitions(-DM_CORE_GBA)
	list(APPEND CORE_SRC
		${ARM_SRC}
		${GBA_SRC}
		${GBA_CHEATS_SRC}
		${GBA_RENDERER_SRC})
	list(APPEND DEBUGGER_SRC
		${CMAKE_CURRENT_SOURCE_DIR}/src/arm/debugger/cli-debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/arm/debugger/debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/arm/debugger/memory-debugger.c
		${CMAKE_CURRENT_SOURCE_DIR}/src/gba/debugger/cli.c)
	list(APPEND TEST_SRC
		${ARM_TEST_SRC}
		${GBA_TEST_SRC})
	if(NOT M_CORE_GB)
		list(APPEND CORE_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/gb/audio.c)
	endif()
endif()

if(USE_DEBUGGERS)
	list(APPEND FEATURE_SRC ${DEBUGGER_SRC})
	list(APPEND TEST_SRC ${DEBUGGER_TEST_SRC})
	list(APPEND FEATURES DEBUGGERS)
endif()

foreach(FEATURE IN LISTS FEATURES)
	list(APPEND FEATURE_DEFINES "USE_${FEATURE}")
endforeach()

foreach(ENABLE IN LISTS ENABLES)
	list(APPEND FEATURE_DEFINES "ENABLE_${ENABLE}")
endforeach()

source_group("Virtual files" FILES ${CORE_VFS_SRC} ${VFS_SRC})
source_group("Extra features" FILES ${FEATURE_SRC})
source_group("Third-party code" FILES ${THIRD_PARTY_SRC})

# Platform binaries
if(DEFINED 3DS)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/3ds ${CMAKE_CURRENT_BINARY_DIR}/3ds)
endif()

if(DEFINED WII)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/wii ${CMAKE_CURRENT_BINARY_DIR}/wii)
endif()

if(DEFINED PSP2)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/psp2 ${CMAKE_CURRENT_BINARY_DIR}/psp2)
endif()

# Binaries
list(APPEND CORE_SRC
	${UTIL_SRC}
	${CORE_VFS_SRC}
	${OS_SRC}
	${THIRD_PARTY_SRC})
list(APPEND TEST_SRC ${UTIL_TEST_SRC})

set(SRC ${CORE_SRC} ${VFS_SRC})
if(NOT MINIMAL_CORE)
	set(ENABLE_EXTRA ON)
	if(M_CORE_GBA)
		list(APPEND SRC
			${GBA_SIO_SRC})
	endif()
	if(M_CORE_GB)
		list(APPEND SRC
			${GB_SIO_SRC})
	endif()
	list(APPEND SRC
		${FEATURE_SRC})
endif()

if(ENABLE_EXTRA)
	if(M_CORE_GBA)
		list(APPEND SRC
			${GBA_RR_SRC}
			${GBA_EXTRA_SRC})
	endif()
	if(M_CORE_GB)
		list(APPEND SRC
			${GB_EXTRA_SRC})
	endif()
	list(APPEND SRC
		${EXTRA_SRC})
endif()

if(NOT SKIP_LIBRARY)
	if(NOT BUILD_STATIC AND NOT BUILD_SHARED)
		set(BUILD_SHARED ON)
	endif()

	if(BUILD_SHARED)
		add_library(${BINARY_NAME} SHARED ${SRC} ${VFS_SRC})
		if(BUILD_STATIC)
			add_library(${BINARY_NAME}-static STATIC ${SRC})
			set_target_properties(${BINARY_NAME}-static PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}" COMPILE_OPTIONS "${FEATURE_FLAGS}")
			install(TARGETS ${BINARY_NAME}-static DESTINATION ${LIBDIR} COMPONENT lib${BINARY_NAME})
			add_dependencies(${BINARY_NAME}-static version-info)
		endif()
	else()
		add_library(${BINARY_NAME} STATIC ${SRC})
	endif()

	add_dependencies(${BINARY_NAME} version-info)
	set_target_properties(${BINARY_NAME} PROPERTIES VERSION ${LIB_VERSION_STRING} SOVERSION ${LIB_VERSION_ABI} COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}" COMPILE_OPTIONS "${FEATURE_FLAGS}")

	target_link_libraries(${BINARY_NAME} ${DEBUGGER_LIB} ${DEPENDENCY_LIB} ${OS_LIB})
	install(TARGETS ${BINARY_NAME} LIBRARY DESTINATION ${LIBDIR} COMPONENT lib${BINARY_NAME} NAMELINK_SKIP ARCHIVE DESTINATION ${LIBDIR} RUNTIME DESTINATION ${LIBDIR} COMPONENT lib${BINARY_NAME})
	if(UNIX AND NOT APPLE AND NOT HAIKU)
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-16.png DESTINATION share/icons/hicolor/16x16/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-24.png DESTINATION share/icons/hicolor/24x24/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-32.png DESTINATION share/icons/hicolor/32x32/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-48.png DESTINATION share/icons/hicolor/48x48/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-64.png DESTINATION share/icons/hicolor/64x64/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-96.png DESTINATION share/icons/hicolor/96x96/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-128.png DESTINATION share/icons/hicolor/128x128/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-256.png DESTINATION share/icons/hicolor/256x256/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
		install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/res/mgba-512.png DESTINATION share/icons/hicolor/512x512/apps RENAME mgba.png COMPONENT lib${BINARY_NAME})
	endif()
else()
	set(BUILD_SHARED OFF)
	set(BUILD_STATIC OFF)
	find_library(${BINARY_NAME} ${BINARY_NAME})
	if(NOT ${BINARY_NAME}_FOUND)
		set(DISABLE_FRONTENDS ON)
		set(BUILD_PERF OFF)
		set(BUILD_TEST OFF)
		set(BUILD_SUITE OFF)
	endif()
endif()

if(BUILD_GL)
	add_definitions(-DBUILD_GL)
endif()

if(BUILD_GLES2)
	add_definitions(-DBUILD_GLES2)
endif()

if(DISABLE_FRONTENDS)
	set(BUILD_SDL OFF)
	set(BUILD_QT OFF)
endif()

if(BUILD_PYTHON)
	enable_testing()
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/python ${CMAKE_CURRENT_BINARY_DIR}/python)
endif()

if(BUILD_LIBRETRO)
	file(GLOB RETRO_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/libretro/*.c)
	add_library(${BINARY_NAME}_libretro SHARED ${CORE_SRC} ${RETRO_SRC})
	set_target_properties(${BINARY_NAME}_libretro PROPERTIES PREFIX "" COMPILE_DEFINITIONS "COLOR_16_BIT;COLOR_5_6_5;DISABLE_THREADING;${OS_DEFINES};${FUNCTION_DEFINES};MINIMAL_CORE=2")
	target_link_libraries(${BINARY_NAME}_libretro ${OS_LIB})
	install(TARGETS ${BINARY_NAME}_libretro LIBRARY DESTINATION ${LIBRETRO_LIBDIR} COMPONENT ${BINARY_NAME}_libretro NAMELINK_SKIP)
endif()

if(BUILD_OPENEMU)
	find_library(FOUNDATION Foundation)
	find_library(OPENEMUBASE OpenEmuBase)
	file(GLOB OE_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/openemu/*.m)
	add_library(${BINARY_NAME}-openemu MODULE ${CORE_SRC} ${OS_SRC})
	set_target_properties(${BINARY_NAME}-openemu PROPERTIES
		MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/openemu/Info.plist.in
		BUNDLE TRUE
		BUNDLE_EXTENSION oecoreplugin
		OUTPUT_NAME ${PROJECT_NAME}
		COMPILE_DEFINITIONS "DISABLE_THREADING;${OS_DEFINES};${FUNCTION_DEFINES};MINIMAL_CORE=1")
	target_link_libraries(${BINARY_NAME}-openemu ${OS_LIB} ${FOUNDATION} ${OPENEMUBASE})
	install(TARGETS ${BINARY_NAME}-openemu LIBRARY DESTINATION ${OE_LIBDIR} COMPONENT ${BINARY_NAME}.oecoreplugin NAMELINK_SKIP)
endif()

if(BUILD_SDL)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/sdl ${CMAKE_CURRENT_BINARY_DIR}/sdl)
	# The SDL platform CMakeLists could decide to disable SDL, so check again before adding the define.
	if(BUILD_SDL)
		add_definitions(-DBUILD_SDL)
	endif()
endif()

if(BUILD_QT)
	add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/src/platform/qt ${CMAKE_CURRENT_BINARY_DIR}/qt)
endif()

if(BUILD_PERF)
	set(PERF_SRC ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/test/perf-main.c)
	if(UNIX AND NOT APPLE)
		list(APPEND PERF_LIB rt)
	endif()

	add_executable(${BINARY_NAME}-perf ${PERF_SRC})
	target_link_libraries(${BINARY_NAME}-perf ${BINARY_NAME} ${PERF_LIB} ${OS_LIB})
	set_target_properties(${BINARY_NAME}-perf PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}")
	install(TARGETS ${BINARY_NAME}-perf DESTINATION bin COMPONENT ${BINARY_NAME}-perf)
	install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/tools/perf.py DESTINATION "${CMAKE_INSTALL_LIBDIR}/${BINARY_NAME}" COMPONENT ${BINARY_NAME}-perf)
endif()

if(BUILD_TEST)
	add_executable(${BINARY_NAME}-fuzz ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/test/fuzz-main.c)
	target_link_libraries(${BINARY_NAME}-fuzz ${BINARY_NAME})
	set_target_properties(${BINARY_NAME}-fuzz PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}")
	add_executable(tbl-fuzz ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/test/tbl-fuzz-main.c)
	target_link_libraries(tbl-fuzz ${BINARY_NAME})
	set_target_properties(tbl-fuzz PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}")
	install(TARGETS ${BINARY_NAME}-fuzz tbl-fuzz DESTINATION bin COMPONENT ${BINARY_NAME}-test)
endif()

if(NOT USE_CMOCKA)
	set(BUILD_SUITE OFF)
endif()
if(BUILD_SUITE)
	enable_testing()
	include_directories(AFTER ${CMOCKA_INCLUDE_DIRS})
	link_directories(${CMOCKA_LIBRARY_DIRS})

	foreach(TEST IN LISTS TEST_SRC)
		string(REPLACE "${CMAKE_SOURCE_DIR}/src/" "" TEST_NAME "${TEST}")
		string(REPLACE "/" "-" TEST_NAME "${TEST_NAME}")
		string(REPLACE "-test" "" TEST_NAME "${TEST_NAME}")
		string(REPLACE ".c" "" TEST_NAME "${TEST_NAME}")
		add_executable(test-${TEST_NAME} ${TEST})
		target_link_libraries(test-${TEST_NAME} ${BINARY_NAME} ${PLATFORM_LIBRARY} cmocka)
		set_target_properties(test-${TEST_NAME} PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}")
		add_test(${TEST_NAME} test-${TEST_NAME})
	endforeach()
endif()

if(BUILD_EXAMPLE)
	add_executable(${BINARY_NAME}-example-server ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/example/client-server/server.c)
	target_link_libraries(${BINARY_NAME}-example-server ${BINARY_NAME})
	set_target_properties(${BINARY_NAME}-example-server PROPERTIES COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}")

	if(BUILD_SDL)
		add_executable(${BINARY_NAME}-example-client ${CMAKE_CURRENT_SOURCE_DIR}/src/platform/example/client-server/client.c)
		target_link_libraries(${BINARY_NAME}-example-client ${BINARY_NAME} ${SDL_LIBRARY} ${SDLMAIN_LIBRARY} ${OPENGL_LIBRARY} ${OPENGLES2_LIBRARY})
		set_target_properties(${BINARY_NAME}-example-client PROPERTIES
		                      COMPILE_DEFINITIONS "${OS_DEFINES};${FEATURE_DEFINES};${FUNCTION_DEFINES}"
		                      INCLUDE_DIRECTORIES "${SDL_INCLUDE_DIR};${CMAKE_CURRENT_SOURCE_DIR}/src;${CMAKE_CURRENT_SOURCE_DIR}/include")
	endif()
endif()

configure_file(${CMAKE_CURRENT_SOURCE_DIR}/src/core/flags.h.in ${CMAKE_CURRENT_BINARY_DIR}/flags.h)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/flags.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/mgba COMPONENT lib${BINARY_NAME})

# Packaging
set(CPACK_PACKAGE_VERSION ${VERSION_STRING})
set(CPACK_PACKAGE_VERSION_MAJOR ${LIB_VERSION_MAJOR})
set(CPACK_PACKAGE_VERSION_MINOR ${LIB_VERSION_MINOR})
set(CPACK_PACKAGE_VERSION_PATCH ${LIB_VERSION_PATCH})
set(CPACK_RESOURCE_FILE_LICENSE ${CMAKE_CURRENT_SOURCE_DIR}/LICENSE)
set(CPACK_RESOURCE_FILE_README ${CMAKE_CURRENT_SOURCE_DIR}/README.md)

set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "mGBA Game Boy Advance Emulator")
set(CPACK_PACKAGE_VENDOR "Jeffrey Pfau")
set(CPACK_PACKAGE_CONTACT "Jeffrey Pfau <jeffrey@endrift.com>")
set(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
set(CPACK_DEBIAN_PACKAGE_SECTION "games")

SET(CPACK_DEB_COMPONENT_INSTALL ON)

set(CPACK_STRIP_FILES ${BINARY_NAME})

file(GLOB READMES ${CMAKE_CURRENT_SOURCE_DIR}/README*.md)
install(FILES ${READMES} ${CMAKE_CURRENT_SOURCE_DIR}/CHANGES DESTINATION ${CMAKE_INSTALL_DOCDIR} COMPONENT lib${BINARY_NAME})

include(CPack)

# Summaries
set(SUMMARY_GL_LIST)
if(USE_EPOXY)
	set(SUMMARY_GL_LIST "libepoxy")
else()
	if(BUILD_GL)
		list(APPEND SUMMARY_GL_LIST "OpenGL")
	endif()
	if(BUILD_GLES2)
		list(APPEND SUMMARY_GL_LIST "OpenGL|ES 2")
	endif()
endif()
if(NOT SUMMARY_GL_LIST)
	set(SUMMARY_GL OFF)
else()
	string(REPLACE ";" ", " SUMMARY_GL "${SUMMARY_GL_LIST}")
endif()
if(USE_LIBZIP)
	set(SUMMARY_ZIP libzip)
elseif(USE_MINIZIP)
	set(SUMMARY_ZIP "minizip (external)")
elseif(USE_ZLIB)
	set(SUMMARY_ZIP "minizip (included)")
else()
	set(SUMMARY_ZIP OFF)
endif()

if(NOT QUIET)
	message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")
	message(STATUS "Platforms:")
	message(STATUS "	Game Boy Advance: ${M_CORE_GBA}")
	message(STATUS "	Game Boy: ${M_CORE_GB}")
	message(STATUS "Features:")
	message(STATUS "	Debuggers: ${USE_DEBUGGERS}")
	if(NOT WIN32)
		message(STATUS "	CLI debugger: ${USE_EDITLINE}")
	endif()
	message(STATUS "	GDB stub: ${USE_GDB_STUB}")
	message(STATUS "	Video recording: ${USE_FFMPEG}")
	message(STATUS "	GIF recording: ${USE_MAGICK}")
	message(STATUS "	Screenshot/advanced savestate support: ${USE_PNG}")
	message(STATUS "	ZIP support: ${SUMMARY_ZIP}")
	message(STATUS "	7-Zip support: ${USE_LZMA}")
	message(STATUS "	SQLite3 game database: ${USE_SQLITE3}")
	message(STATUS "	ELF loading support: ${USE_ELF}")
	message(STATUS "	OpenGL support: ${SUMMARY_GL}")
	message(STATUS "Frontends:")
	message(STATUS "	Qt: ${BUILD_QT}")
	message(STATUS "	SDL (${SDL_VERSION}): ${BUILD_SDL}")
	message(STATUS "	Profiling: ${BUILD_PERF}")
	message(STATUS "	Test harness: ${BUILD_TEST}")
	message(STATUS "	Test suite: ${BUILD_SUITE}")
	message(STATUS "	Python bindings: ${BUILD_PYTHON}")
	message(STATUS "	Examples: ${BUILD_EXAMPLE}")
	message(STATUS "Cores:")
	message(STATUS "	Libretro core: ${BUILD_LIBRETRO}")
	if(APPLE)
		message(STATUS "	OpenEmu core: ${BUILD_OPENEMU}")
	endif()
	message(STATUS "Libraries:")
	message(STATUS "	Static: ${BUILD_STATIC}")
	message(STATUS "	Shared: ${BUILD_SHARED}")
endif()
